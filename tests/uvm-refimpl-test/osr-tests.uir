// requires "primitives.uir"

.funcsig @v_i = (@i64) -> ()

.funcdef @intro_test_base VERSION @intro_test_base_v1 <@v_i> {
    %entry(<@i64> %n):
        %rv = [%call] CALL <@i_i> @intro_rec (%n)
        
        COMMINST @uvm.thread_exit
}

.funcdef @intro_rec VERSION @intro_rec_v1 <@i_i> {
    %entry(<@i64> %n):
        %isz = EQ <@i64> %n @I64_0
        BRANCH2 %isz %zero(%n) %nz(%n)

    %zero(<@i64> %n):
        %v = [%trap_rec] TRAP <@i64> KEEPALIVE(%n)
        RET %v

    %nz(<@i64> %n):
        %nm1 = SUB <@i64> %n @I64_1
        %rv = [%call] CALL <@i_i> @intro_rec (%nm1) KEEPALIVE(%n %nm1)

        RET %rv
}

.funcdef @osr_test_base VERSION @osr_test_base_v1 <@v_i> {
    %entry(<@i64> %n):
        %rv = [%call] CALL <@i_i> @sum (%n)
        [%trap_base_exit] TRAP <> KEEPALIVE (%rv)
        COMMINST @uvm.thread_exit
}

.const @sum_v1.THRESHOLD <@i64> = 5

// Sum from 0..(n-1), but optimise when adding more than 5 numbers
.funcdef @sum VERSION @sum_v1 <@i_i> {
    %entry(<@i64> %n):
        BRANCH %head(%n @I64_0 @I64_0)

    %head(<@i64> %n <@i64> %i <@i64> %s):
        %ge_thr = SGE <@i64> %i @sum_v1.THRESHOLD
        BRANCH2 %ge_thr %opt(%n %i %s) %head2(%n %i %s)

    %head2(<@i64> %n <@i64> %i <@i64> %s):
        %lt_n = SLT <@i64> %i %n
        BRANCH2 %lt_n %body(%n %i %s) %exit(%s)

    %body(<@i64> %n <@i64> %i <@i64> %s):
        %i2 = ADD <@i64> %i @I64_1
        %s2 = ADD <@i64> %s %i
        BRANCH %head(%n %i2 %s2)

    %exit(<@i64> %s):
        RET %s

    %opt(<@i64> %n <@i64> %i <@i64> %s):
        [%trap_opt] TRAP <> KEEPALIVE(%n %i %s)
        THROW @NULLREF // unreachable
}

.funcdef @consecutive_push_main VERSION %v1 <@i_i> {
    %entry(<@i64> %n):
        [%trap] TRAP <> KEEPALIVE(%n)
        COMMINST @uvm.thread_exit
}

.const @I64_42 <@i64> = 42

.funcdef @forty_two_returner VERSION %v1 <@v_v> {
    %entry():
        RET @I64_42
}

.funcdef @add_one VERSION %v1 <@i_i> {
    %entry(<@i64> %n):
        %n2 = ADD <@i64> %n @I64_1
        [%trap] TRAP <> KEEPALIVE(%n %n2)
        RET %n2
}
